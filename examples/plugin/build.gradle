plugins {
    id "java"
    id "io.spring.dependency-management"
}

sourceCompatibility = targetCompatibility = 8

test {
    useJUnit()
}

jar {
    baseName = "example-plugin"
    manifest {
        attributes(
                'Plugin-Id': "example-plugin",
                'Plugin-Version': "1.0.0",
        )
    }

    into('lib') {
        from configurations.compile
    }
}

sourceSets.test.resources.srcDir jar.outputs.files.singleFile.parentFile
test {
    dependsOn jar
}

repositories {
    jcenter()
    maven { url 'https://jitpack.io' }
}

dependencyManagement {
    overriddenByDependencies = false

    imports {
        mavenBom org.springframework.boot.gradle.plugin.SpringBootPlugin.BOM_COORDINATES
        mavenBom 'org.testcontainers:testcontainers-bom:1.12.3'
    }

    dependencies {
        dependencySet(group: "com.github.bsideup.liiklus", version: "0.9.0") {
            entry "api"
            entry "client"
            entry "testing"
        }

        dependency 'org.projectlombok:lombok:1.18.10'
        dependency 'com.google.auto.service:auto-service:1.0-rc6'
    }
}

dependencies {
    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'
    compileOnly "com.google.auto.service:auto-service"
    annotationProcessor "com.google.auto.service:auto-service"

    compileOnly "com.github.bsideup.liiklus:api"
    compileOnly 'org.springframework.boot:spring-boot-starter'
    compileOnly 'io.projectreactor:reactor-core'

    compile 'org.apache.commons:commons-lang3:3.9'

    testCompile 'org.testcontainers:kafka'
    testCompile 'ch.qos.logback:logback-classic:'
    testCompile "com.github.bsideup.liiklus:client"
    testCompile "com.github.bsideup.liiklus:testing"
    testCompile 'org.assertj:assertj-core'
}
