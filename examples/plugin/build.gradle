plugins {
    id 'java-library'
    id 'io.spring.dependency-management'
    id 'io.franzbecker.gradle-lombok'
}

sourceCompatibility = targetCompatibility = 8

jar {
    archiveBaseName = "example-plugin"
    manifest {
        attributes(
                'Plugin-Id': "example-plugin",
                'Plugin-Version': "1.0.0",
        )
    }

    into('lib') {
        from configurations.runtimeClasspath - configurations.compileOnlyClasspath
    }
}

configurations {
    compileOnlyClasspath {
        extendsFrom compileOnly
        canBeConsumed false
        canBeResolved true
    }
}

processTestResources.dependsOn(jar)
test.dependsOn(jar)

sourceSets {
    test {
        resources.srcDir jar.outputs.files.singleFile.parentFile
    }
}

repositories {
    mavenCentral()
    maven { url 'https://jitpack.io' }
}

dependencyManagement {
    overriddenByDependencies = false

    imports {
        mavenBom org.springframework.boot.gradle.plugin.SpringBootPlugin.BOM_COORDINATES
        mavenBom 'org.testcontainers:testcontainers-bom:1.17.0'
    }

    dependencies {
        dependencySet(group: "com.github.bsideup.liiklus", version: "0.9.0") {
            entry "api"
            entry "client"
            entry "testing"
        }

        dependency 'com.google.auto.service:auto-service:1.0'
    }
}

dependencies {
    compileOnly "com.google.auto.service:auto-service"
    annotationProcessor "com.google.auto.service:auto-service"

    compileOnly "com.github.bsideup.liiklus:api"
    compileOnly 'org.springframework.boot:spring-boot-starter'
    compileOnly 'io.projectreactor:reactor-core'

    implementation 'org.apache.commons:commons-lang3:3.9'

    testImplementation 'org.testcontainers:kafka'
    testImplementation 'ch.qos.logback:logback-classic:'
    testImplementation "com.github.bsideup.liiklus:client"
    testImplementation "com.github.bsideup.liiklus:testing"
    testImplementation 'org.assertj:assertj-core'
}
