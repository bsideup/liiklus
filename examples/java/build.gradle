plugins {
    id "java"
    id "idea"
    id "com.google.protobuf"
}

protobuf {
    protoc {
        artifact = 'com.google.protobuf:protoc:3.10.1'
    }

    generatedFilesBaseDir = "$projectDir/generated"

    plugins {
        grpc {
            artifact = 'io.grpc:protoc-gen-grpc-java:1.24.1'
        }

        reactor {
            artifact = "com.salesforce.servicelibs:reactor-grpc:0.8.2:jdk8@jar"
        }
    }

    generateProtoTasks {
        ofSourceSet('main')*.plugins {
            grpc { }
            reactor { }
        }
    }
}

clean {
    delete protobuf.generatedFilesBaseDir
}

idea {
    module {
        generatedSourceDirs += file("${protobuf.generatedFilesBaseDir}/main/java")
        generatedSourceDirs += file("${protobuf.generatedFilesBaseDir}/main/reactor")
        generatedSourceDirs += file("${protobuf.generatedFilesBaseDir}/main/grpc")
    }
}

repositories {
    jcenter()
}

dependencies {
    compileOnly 'org.projectlombok:lombok:1.18.10'
    annotationProcessor 'org.projectlombok:lombok:1.18.8'

    compile 'org.testcontainers:kafka:1.12.3'

    compile 'org.apache.commons:commons-math3:3.6.1'

    compile 'com.google.protobuf:protobuf-java:3.10.0'

    compile 'io.grpc:grpc-netty:1.24.1'
    compile 'io.grpc:grpc-protobuf:1.24.1'
    compile 'com.salesforce.servicelibs:reactor-grpc-stub:0.10.0'

    runtime 'ch.qos.logback:logback-classic:1.2.3'
}