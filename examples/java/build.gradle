plugins {
    id 'java'
    id 'idea'
    id 'com.google.protobuf'
    id 'io.franzbecker.gradle-lombok'
}

protobuf {
    protoc {
        artifact = 'com.google.protobuf:protoc:3.10.1'
    }

    generatedFilesBaseDir = "$projectDir/generated"

    plugins {
        grpc {
            artifact = 'io.grpc:protoc-gen-grpc-java:1.24.1'
        }

        reactor {
            artifact = "com.salesforce.servicelibs:reactor-grpc:0.8.2:jdk8@jar"
        }
    }

    generateProtoTasks {
        ofSourceSet('main')*.plugins {
            grpc { }
            reactor { }
        }
    }
}

clean {
    delete protobuf.generatedFilesBaseDir
}

idea {
    module {
        generatedSourceDirs += file("${protobuf.generatedFilesBaseDir}/main/java")
        generatedSourceDirs += file("${protobuf.generatedFilesBaseDir}/main/reactor")
        generatedSourceDirs += file("${protobuf.generatedFilesBaseDir}/main/grpc")
    }
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.testcontainers:kafka:1.15.3'

    implementation 'org.apache.commons:commons-math3:3.6.1'

    implementation 'com.google.protobuf:protobuf-java:3.10.0'

    implementation 'io.grpc:grpc-netty:1.24.1'
    implementation 'io.grpc:grpc-protobuf:1.46.0'
    implementation 'com.salesforce.servicelibs:reactor-grpc-stub:0.10.0'

    runtimeOnly 'ch.qos.logback:logback-classic:1.2.4-groovyless'
}