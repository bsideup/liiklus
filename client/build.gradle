plugins {
    id 'idea'
    id 'java-library'
    id 'com.google.protobuf'
}

protobuf {
    protoc {
        artifact = 'com.google.protobuf:protoc'
    }

    generatedFilesBaseDir = "$projectDir/generated"

    plugins {
        grpc {
            artifact = 'io.grpc:protoc-gen-grpc-java'
        }

        reactor {
            artifact = 'com.salesforce.servicelibs:reactor-grpc:0.9.0:jdk8@jar'
        }

        rsocketRpc {
            artifact = 'io.rsocket.rpc:rsocket-rpc-protobuf'
        }
    }

    generateProtoTasks {
        ofSourceSet('main').each { task ->
            task.builtins {
                remove java
            }
            task.plugins {
                grpc { }
                reactor { }
                rsocketRpc { }
            }
        }
    }
}

clean {
    delete protobuf.generatedFilesBaseDir
}

java {
    withSourcesJar()
}

idea {
    module {
        generatedSourceDirs += file("${protobuf.generatedFilesBaseDir}/main/java")
        generatedSourceDirs += file("${protobuf.generatedFilesBaseDir}/main/reactor")
        generatedSourceDirs += file("${protobuf.generatedFilesBaseDir}/main/grpc")
        generatedSourceDirs += file("${protobuf.generatedFilesBaseDir}/main/rsocketRpc")
    }
}

sourceCompatibility = targetCompatibility = 8

dependencies {
    protobuf project(":protocol")
    api project(":protocol")

    api 'io.grpc:grpc-stub'
    api 'io.grpc:grpc-protobuf'
    api 'com.salesforce.servicelibs:reactor-grpc-stub'
    api 'io.rsocket.rpc:rsocket-rpc-core'

    api 'io.rsocket:rsocket-transport-netty'
    api 'io.grpc:grpc-netty'
    api 'com.google.protobuf:protobuf-java-util'
}