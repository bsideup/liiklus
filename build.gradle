buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:2.1.0.RELEASE")
    }
}

plugins {
    id "com.google.protobuf" version "0.8.4" apply false
}

allprojects {
    repositories {
        jcenter()
        maven { url "https://repo.spring.io/snapshot" }
        maven { url "https://repo.spring.io/milestone" }
        maven { url 'https://jitpack.io' }
    }

    task downloadDependencies {
        doLast {
            configurations.all {
                try {
                    it.files
                } catch (e) {
                    project.logger.info(e.message)
                }
            }
        }
    }
}

configure(subprojects.findAll { !it.name.startsWith("examples/") }) {
    apply plugin: 'io.spring.dependency-management'
    apply plugin: 'maven'
    apply plugin: 'java'

    sourceCompatibility = targetCompatibility = 10

    test.testLogging {
        displayGranularity 1
        showStackTraces = true
        exceptionFormat = 'full'
        events "STARTED", "PASSED", "FAILED", "SKIPPED"
    }

    project.afterEvaluate {
        project.tasks.findByName("install")?.dependsOn(tasks.findByName("assemble"))
    }

    dependencyManagement {
        overriddenByDependencies = false

        imports {
            mavenBom 'org.junit:junit-bom:5.3.1'
            mavenBom org.springframework.boot.gradle.plugin.SpringBootPlugin.BOM_COORDINATES
            mavenBom 'org.testcontainers:testcontainers-bom:1.10.1'
        }

        dependencies {
            dependency 'org.projectlombok:lombok:1.18.4'

            dependency 'org.pf4j:pf4j:2.4.0'

            dependencySet(group: 'io.rsocket', version: '0.11.12') {
                entry 'rsocket-transport-netty'
                entry 'rsocket-core'
            }

            dependencySet(group: 'io.rsocket.rpc', version: '0.2.5') {
                entry 'rsocket-rpc-core'
                entry 'rsocket-rpc-protobuf'
            }

            dependency 'io.prometheus:simpleclient_common:0.5.0'

            dependency 'org.springframework.fu:spring-fu-jafu:0.0.3.BUILD-SNAPSHOT'

            dependencySet(group: 'com.google.protobuf', version: '3.6.1') {
                entry 'protoc'
                entry 'protobuf-java'
                entry 'protobuf-java-util'
            }

            dependency 'com.google.auto.service:auto-service:1.0-rc4'

            // Override kafka-clients' version, otherwise it comes from Spring Boot
            dependency 'org.apache.kafka:kafka-clients:2.0.0'

            dependencySet(group: 'io.grpc', version: '1.15.1') {
                entry 'grpc-netty'
                entry 'grpc-core'
                entry 'grpc-services'
                entry 'grpc-protobuf'
                entry 'grpc-stub'
                entry 'protoc-gen-grpc-java'
            }

            dependency 'com.salesforce.servicelibs:reactor-grpc-stub:0.9.0'

            dependency 'org.awaitility:awaitility:3.1.2'
        }
    }
}
