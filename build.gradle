plugins {
    id "org.springframework.boot" version "2.0.2.RELEASE" apply false
    id "com.google.protobuf" version "0.8.4" apply false
}

allprojects {
    task downloadDependencies {
        doLast {
            configurations.all {
                try {
                    it.files
                } catch (e) {
                    project.logger.info(e.message)
                }
            }
        }
    }
}

configure(subprojects.findAll { !it.name.startsWith("examples/") }) {
    apply plugin: 'io.spring.dependency-management'
    apply plugin: 'maven'

    repositories {
        jcenter()
        maven { url 'https://jitpack.io' }
    }

    project.afterEvaluate {
        project.tasks.findByName("install")?.dependsOn(tasks.findByName("assemble"))
    }

    configurations.all {
        resolutionStrategy.dependencySubstitution {
            // FIXME after 2.3.0 is out
            substitute module('org.pf4j:pf4j') with module('com.github.pf4j.pf4j:pf4j:f80bc959c1')
            substitute module('io.projectreactor.kafka:reactor-kafka') with module('com.github.reactor:reactor-kafka:1653062')
        }
    }

    dependencyManagement {
        overriddenByDependencies = false

        imports {
            mavenBom 'org.springframework.boot:spring-boot-dependencies:2.0.2.RELEASE'
            mavenBom 'org.testcontainers:testcontainers-bom:1.8.0'
        }

        dependencies {
            dependency 'org.lognet:grpc-spring-boot-starter:2.3.2'

            dependencySet(group: 'com.google.protobuf', version: '3.6.0') {
                entry 'protoc'
                entry 'protobuf-java'
                entry 'protobuf-java-util'
            }

            dependency 'com.google.auto.service:auto-service:1.0-rc4'

            dependency 'io.projectreactor.kafka:reactor-kafka:1.0.0.RELEASE'

            dependencySet(group: 'io.grpc', version: '1.13.1') {
                entry 'grpc-netty'
                entry 'grpc-core'
                entry 'grpc-services'
                entry 'grpc-protobuf'
                entry 'grpc-stub'
                entry 'protoc-gen-grpc-java'
            }

            dependency 'com.salesforce.servicelibs:reactor-grpc-stub:0.9.0'

            dependency 'org.awaitility:awaitility:3.0.0'
        }
    }
}
