plugins {
    id "org.springframework.boot" version "2.0.0.RELEASE" apply false
    id "com.google.protobuf" version "0.8.4" apply false
}

configure(subprojects.findAll { !it.name.startsWith("examples") }) {
    apply plugin: 'io.spring.dependency-management'
    apply plugin: 'maven'

    repositories {
        jcenter()
        maven { url 'https://jitpack.io' }
    }

    project.afterEvaluate {
        project.tasks.findByName("install")?.dependsOn(tasks.findByName("assemble"))
    }

    configurations.all {
        resolutionStrategy.dependencySubstitution {
            substitute module('org.gradle:api') with project(':api')
            substitute project(':util') with module('org.gradle:util:3.0')

            // FIXME after 2.3.0 is out
            substitute module('org.pf4j:pf4j') with module('com.github.pf4j.pf4j:pf4j:f80bc959c1')
        }
    }

    dependencyManagement {
        overriddenByDependencies = false

        imports {
            mavenBom 'org.springframework.boot:spring-boot-dependencies:2.0.0.RELEASE'
            mavenBom 'org.testcontainers:testcontainers-bom:1.7.2'
        }

        dependencies {
            dependency 'org.lognet:grpc-spring-boot-starter:2.1.5'

            dependencySet(group: 'com.google.protobuf', version: '3.5.1') {
                entry 'protoc'
                entry 'protobuf-java'
            }

            dependency 'com.google.auto.service:auto-service:1.0-rc4'

            dependency 'io.projectreactor.kafka:reactor-kafka:1.0.0.RELEASE'

            dependencySet(group: 'io.grpc', version: '1.11.0') {
                entry 'grpc-netty'
                entry 'grpc-core'
                entry 'grpc-services'
                entry 'grpc-protobuf'
                entry 'grpc-stub'
                entry 'protoc-gen-grpc-java'
            }

            dependency 'com.salesforce.servicelibs:reactor-grpc-stub:0.8.1'

            dependency 'org.awaitility:awaitility:3.0.0'
        }
    }
}
