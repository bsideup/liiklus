jar {
    manifest {
        attributes(
                'Plugin-Id': "${project.name}",
                'Plugin-Version': "${project.version}",
                'Plugin-Dependencies': [
                        project(":plugins:grpc-transport").name
                ].join(","),
        )
    }

    into('lib') {
        from(configurations.compile - configurations.compileOnly)
    }
}

tasks.test.dependsOn(jar)
tasks.test.dependsOn(
        [":plugins:inmemory-records-storage", ":plugins:inmemory-positions-storage", ":plugins:grpc-transport"].collect {
            project(it).getTasksByName("jar", true)
        }
)

dependencies {
    compileOnly 'com.google.auto.service:auto-service'
    annotationProcessor 'com.google.auto.service:auto-service'

    compileOnly project(":app")
    compileOnly project(":plugins:grpc-transport")

    compile 'com.auth0:java-jwt:3.9.0'
    compile 'com.avast.grpc.jwt:grpc-java-jwt:0.4.12'

    // see https://github.com/grpc/grpc-java/blob/master/SECURITY.md#netty for the compatibility version
    runtimeOnly 'io.netty:netty-tcnative-boringssl-static:2.0.25.Final'
    testRuntimeOnly 'io.netty:netty-tcnative-boringssl-static:2.0.25.Final'

    testCompile project(":tck")
    testCompile project(":client")
    testCompile 'org.springframework.boot:spring-boot-starter-test'
    testCompile 'org.bouncycastle:bcpkix-jdk15on:1.66'
    testRuntime project(":plugins:grpc-transport")
}
