jar {
    manifest {
        attributes(
                'Plugin-Id': "${project.name}",
                'Plugin-Version': "${project.version}",
        )
    }

    into('lib') {
        from(configurations.compile - configurations.compileOnly)
    }
}

sourceSets {
    e2eTest
}

task e2eTest(type: Test) {
    group = 'verification'
    testClassesDirs = sourceSets.e2eTest.output.classesDirs
    classpath = sourceSets.e2eTest.runtimeClasspath
}
tasks.check.dependsOn(e2eTest)

e2eTest {
    useJUnitPlatform()
}

tasks.e2eTest.dependsOn(
        jar,
        rootProject.project(":inmemory-positions-storage").getTasksByName("jar", false)
)

dependencies {
    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'
    compileOnly 'com.google.auto.service:auto-service'
    annotationProcessor 'com.google.auto.service:auto-service'

    compileOnly project(":app")

    compile 'org.apache.kafka:kafka-clients'

    e2eTestCompile 'org.projectlombok:lombok'
    e2eTestAnnotationProcessor 'org.projectlombok:lombok'
    e2eTestRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine'
    e2eTestCompile project(":app")
    e2eTestCompile project(":tck")
    e2eTestCompile 'org.testcontainers:kafka'
}