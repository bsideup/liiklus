plugins {
    id 'idea'
    id 'java-library'
    id 'com.google.protobuf'
}

protobuf {
    protoc {
        artifact = 'com.google.protobuf:protoc'
    }

    generatedFilesBaseDir = "$projectDir/generated"

    plugins {
        grpc {
            artifact = 'io.grpc:protoc-gen-grpc-java'
        }

        reactor {
            artifact = "com.salesforce.servicelibs:reactor-grpc:0.9.0:jdk8@jar"
        }
    }

    generateProtoTasks {
        ofSourceSet('main').each { task ->
            task.builtins {
                remove java
            }
            task.plugins {
                grpc { }
                reactor { }
            }
        }
    }
}

clean {
    delete protobuf.generatedFilesBaseDir
}

idea {
    module {
        generatedSourceDirs += file("${protobuf.generatedFilesBaseDir}/main/java")
        generatedSourceDirs += file("${protobuf.generatedFilesBaseDir}/main/grpc")
        generatedSourceDirs += file("${protobuf.generatedFilesBaseDir}/main/reactor")
    }
}

jar {
    manifest {
        attributes(
                'Plugin-Id': "${project.name}",
                'Plugin-Version': "${project.version}",
        )
    }

    into('lib') {
        from(configurations.runtimeClasspath - configurations.compileOnlyClasspath)
    }
}

dependencies {
    compileOnly 'com.google.auto.service:auto-service'
    annotationProcessor 'com.google.auto.service:auto-service'

    compileOnly project(":app")

    protobuf project(":protocol")

    api 'com.salesforce.servicelibs:reactor-grpc-stub'
    api 'io.grpc:grpc-stub'
    api 'io.grpc:grpc-protobuf'
    api 'io.grpc:grpc-netty'
    api 'io.grpc:grpc-services'

    testImplementation project(":app")
    testImplementation 'org.springframework.boot:spring-boot-test'
    testImplementation 'org.assertj:assertj-core'
    testImplementation 'org.mockito:mockito-core'
}
