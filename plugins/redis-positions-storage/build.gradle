jar {
    manifest {
        attributes(
                'Plugin-Id': "${project.name}",
                'Plugin-Version': "${project.version}",
        )
    }

    into('lib') {
        from(configurations.compile - configurations.provided)
    }
}

sourceSets {
    e2eTest
}

task e2eTest(type: Test) {
    group = 'verification'
    testClassesDirs = sourceSets.e2eTest.output.classesDirs
    classpath = sourceSets.e2eTest.runtimeClasspath
}
tasks.check.dependsOn(e2eTest)

e2eTest {
    useJUnitPlatform()
}

dependencies {
    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'
    compileOnly 'com.google.auto.service:auto-service'
    annotationProcessor 'com.google.auto.service:auto-service'

    provided project(":app")

    compile 'io.lettuce:lettuce-core'

    testCompileOnly 'org.projectlombok:lombok'
    testAnnotationProcessor 'org.projectlombok:lombok'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine'
    testCompile project(":tck"), {
        exclude group: "org.slf4j", module: "slf4j-simple"
    }
    testCompile 'org.springframework.boot:spring-boot-test'

    e2eTestCompile 'org.projectlombok:lombok'
    e2eTestAnnotationProcessor 'org.projectlombok:lombok'
    e2eTestRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine'
    e2eTestCompile project(":app")
    e2eTestCompile project(":tck"), {
        exclude group: "org.slf4j", module: "slf4j-simple"
    }
    e2eTestCompile 'org.testcontainers:testcontainers'
    e2eTestCompile 'org.assertj:assertj-core'
    e2eTestCompile 'io.lettuce:lettuce-core'
}

tasks.e2eTest.dependsOn(
        jar,
        rootProject.project(":inmemory-records-storage").getTasksByName("jar", false)
)